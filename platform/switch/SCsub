#!/usr/bin/env python
import version
import os

# Godot platform specifics

Import("env")

files = [
    "godot_switch.cpp",
    "os_switch.cpp",
]

program = env.add_program("#bin/godot", files)
simple_console = env.add_program("#bin/simple_console", "simple_console.cpp")

# Switch specifics
# TODO:vrince make is magic on windows/mac
devkitpro_path = os.environ.get("DEVKITPRO", "/opt/devkitpro")
nacptool_path = devkitpro_path + "/tools/bin/nacptool"
elf2nro_path = devkitpro_path + "/tools/bin/elf2nro"

icon_path = "platform/switch/icon.jpg"
nacp = "_default.nacp"
nacp_path = "platform/switch/" + nacp
romfsdir_path = "platform/switch/romfs"

build_target = env["target"].replace("template_", "")  # to be consistent with other godot targets
version_str = "{}.{}.{}-{}".format(version.major, version.minor, version.patch, version.status)


def build_nro(prog_name, output_name, title):
    """
    Helper function to build nacp, nro and make sure they are build in proper order
    """
    nacp = output_name + ".nacp"
    nro = "#bin/" + output_name + ".nro"

    # generate nacp with dummy info
    env.Command(nacp, None, nacptool_path + " --create '" + title + "' 'Godot Team' '" + version_str + "' $TARGET")
    env.AlwaysBuild(nacp)

    # build nro (ref https://github.com/switchbrew/switch-tools/blob/master/src/elf2nro.c)
    env.Command(
        nro,
        prog_name,
        elf2nro_path
        + " $SOURCE $TARGET --icon="
        + icon_path
        + " --nacp=platform/switch/"
        + output_name
        + ".nacp"
        + " --romfsdir="
        + romfsdir_path,
    )
    env.AlwaysBuild(nro)

    # make sure default nro is ready before build nro target
    env.Depends(target=nro, dependency=nacp)


build_nro(program, "switch_" + build_target, "Godot Switch (" + build_target + ")")
build_nro(simple_console, "simple_console", "Simple Console")


# ref : https://github.com/switchbrew/switch-tools
# ref : https://switchbrew.org/wiki/Setting_up_Development_Environment

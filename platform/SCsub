#!/usr/bin/env python

import methods

Import("env")

env.platform_sources = []

# Register platform-exclusive APIs
reg_apis_inc = '#include "register_platform_apis.h"\n'
reg_apis = "void register_platform_apis() {\n"
unreg_apis = "void unregister_platform_apis() {\n"
for platform in env.platform_apis:
    platform_dir = env.Dir(platform)
    env.add_source_files(env.platform_sources, platform + "/api/api.cpp")
    reg_apis += "\tregister_" + platform + "_api();\n"
    unreg_apis += "\tunregister_" + platform + "_api();\n"
    reg_apis_inc += '#include "' + platform + '/api/api.h"\n'
reg_apis_inc += "\n"
reg_apis += "}\n\n"
unreg_apis += "}\n"

methods.write_file_if_needed("register_platform_apis.gen.cpp", reg_apis_inc + reg_apis + unreg_apis)
env.add_source_files(env.platform_sources, "register_platform_apis.gen.cpp")

lib = env.add_library("platform", env.platform_sources)
env.Prepend(LIBS=[lib])

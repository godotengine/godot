#!/usr/bin/env python

from methods import generate_modules_enabled

Import('env')

env_modules = env.Clone()

Export('env_modules')

env.modules_sources = []

env_modules.add_source_files(env.modules_sources, "register_module_types.gen.cpp")

for x in env.module_list:
    if (x in env.disabled_modules):
        continue
    env_modules.Append(CPPDEFINES=["MODULE_" + x.upper() + "_ENABLED"])
    SConscript(x + "/SCsub")

env.Command("modules_enabled.gen.h", Value([sorted(env.module_list), sorted(env.disabled_modules)]), generate_modules_enabled)

if env.split_modules:
    env.split_lib("modules", env_lib = env_modules)
else:
    lib = env_modules.add_library("modules", env.modules_sources)

    env.Prepend(LIBS=[lib])

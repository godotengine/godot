#!/usr/bin/env python
from misc.utility.scons_hints import *

Import("env")
Import("env_modules")

env_betsy = env_modules.Clone()

shader_files = ["alpha_stitch.glsl", "bc1.glsl", "bc4.glsl", "bc6h.glsl"]
for file in shader_files:
    env_betsy.GLSL_HEADER(f"#modules/betsy/{file}")
env_betsy.Depends(Glob(f"#{env['build_dir']}/modules/betsy/*.glsl.gen.h"), ["#glsl_builders.py"])

env_betsy.Append(CPPPATH=[f"#{env['build_dir']}/modules/betsy"])

# Thirdparty source files
thirdparty_obj = []
thirdparty_dir = "#thirdparty/betsy/"
env_betsy.Prepend(CPPPATH=[thirdparty_dir])

env_thirdparty = env_betsy.Clone()
env_thirdparty.disable_warnings()
env.modules_sources += thirdparty_obj

# Godot source files
module_obj = []
env_betsy.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)

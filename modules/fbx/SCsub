#!/usr/bin/env python
from misc.utility.scons_hints import *

Import("env")
Import("env_modules")

env_fbx = env_modules.Clone()

# Thirdparty source files

thirdparty_obj = []

thirdparty_dir = "#thirdparty/ufbx/"
# ufbx_write includes ufbx, so we need both
ufbx_sources = [thirdparty_dir + "test/ufbx/ufbx.c"]
ufbx_write_sources = [thirdparty_dir + "ufbx_write.c"]
thirdparty_sources = ufbx_sources + ufbx_write_sources

env_fbx.Prepend(CPPPATH=[thirdparty_dir])
env_fbx.Prepend(CPPPATH=[thirdparty_dir + "test/ufbx"])

env_thirdparty = env_fbx.Clone()
env_thirdparty.disable_warnings()

env_thirdparty.Append(
    CPPDEFINES=[
        "UFBX_NO_SUBDIVISION",
        "UFBX_NO_TESSELLATION",
        "UFBX_NO_GEOMETRY_CACHE",
        "UFBX_NO_SCENE_EVALUATION",
        "UFBX_NO_INDEX_GENERATION",
        "UFBX_NO_SKINNING_EVALUATION",
        "UFBX_NO_FORMAT_OBJ",
        "UFBX_HEADER_PATH=<test/ufbx/ufbx.h>",  # ufbx_write.c needs to know where ufbx.h is
    ]
)

# ufbx_write is now always available since we're using it to replace ufbx
env_thirdparty.Append(CPPDEFINES=["UFBX_WRITE_AVAILABLE"])

env_thirdparty.add_source_files(thirdparty_obj, thirdparty_sources)
env.modules_sources += thirdparty_obj

# Godot source files

module_obj = []

env_fbx.add_source_files(module_obj, "*.cpp")
env_fbx.add_source_files(module_obj, "structures/*.cpp")

SConscript("extensions/SCsub")

if env.editor_build:
    env_fbx.add_source_files(module_obj, "editor/*.cpp")

env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)

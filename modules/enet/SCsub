#!/usr/bin/env python

Import("env")
Import("env_modules")

env_enet = env_modules.Clone()

# Thirdparty source files

thirdparty_obj = []

if env["builtin_enet"]:
    thirdparty_dir = "#thirdparty/enet/"
    thirdparty_sources = [
        "godot.cpp",
        "callbacks.c",
        "compress.c",
        "host.c",
        "list.c",
        "packet.c",
        "peer.c",
        "protocol.c",
    ]
    thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

    env_enet.Prepend(CPPPATH=[thirdparty_dir])
    env_enet.Append(CPPDEFINES=["GODOT_ENET"])

    env_thirdparty = env_enet.Clone()
    env_thirdparty.disable_warnings()
    env_thirdparty.add_source_files(thirdparty_obj, thirdparty_sources)
    env.modules_sources += thirdparty_obj


# Godot source files

module_obj = []

env_enet.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)

#!/usr/bin/env python

Import('env')
Import('env_modules')

env_etc = env_modules.Clone()

# Thirdparty source files
# Not unbundled so far since not widespread as shared library
thirdparty_dir = "#thirdparty/etc2comp/"
thirdparty_sources = [
	"EtcBlock4x4.cpp",
	"EtcBlock4x4Encoding.cpp",
	"EtcBlock4x4Encoding_ETC1.cpp",
	"EtcBlock4x4Encoding_R11.cpp",
	"EtcBlock4x4Encoding_RG11.cpp",
	"EtcBlock4x4Encoding_RGB8A1.cpp",
	"EtcBlock4x4Encoding_RGB8.cpp",
	"EtcBlock4x4Encoding_RGBA8.cpp",
	"Etc.cpp",
	"EtcDifferentialTrys.cpp",
	"EtcFilter.cpp",
	"EtcImage.cpp",
	"EtcIndividualTrys.cpp",
	"EtcMath.cpp",
	"EtcSortedBlockList.cpp",
]
thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_etc.Prepend(CPPPATH=[thirdparty_dir])

# upstream uses c++11
if not env.msvc:
	env_etc.Append(CXXFLAGS="-std=c++11")

env_thirdparty = env_etc.Clone()
env_thirdparty.disable_warnings()
env_thirdparty.add_source_files(env.modules_sources, thirdparty_sources)

# Godot source files
env_etc.add_source_files(env.modules_sources, "*.cpp")

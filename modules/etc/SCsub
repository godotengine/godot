#!/usr/bin/env python

Import('env')
Import('env_modules')

env_etc = env_modules.Clone()

# Thirdparty source files
# Not unbundled so far since not widespread as shared library
thirdparty_dir = "#thirdparty/etc2comp/"
thirdparty_sources = [
	"EtcBlock4x4.cpp",
	"EtcBlock4x4Encoding.cpp",
	"EtcBlock4x4Encoding_ETC1.cpp",
	"EtcBlock4x4Encoding_R11.cpp",
	"EtcBlock4x4Encoding_RG11.cpp",
	"EtcBlock4x4Encoding_RGB8A1.cpp",
	"EtcBlock4x4Encoding_RGB8.cpp",
	"EtcBlock4x4Encoding_RGBA8.cpp",
	"Etc.cpp",
	"EtcDifferentialTrys.cpp",
	"EtcFilter.cpp",
	"EtcImage.cpp",
	"EtcIndividualTrys.cpp",
	"EtcMath.cpp",
	"EtcSortedBlockList.cpp",
]
thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_etc.add_source_files(env.modules_sources, thirdparty_sources)
env_etc.Append(CPPPATH=[thirdparty_dir])

# Godot source files
env_etc.add_source_files(env.modules_sources, "*.cpp")

# upstream uses c++11
if (not env_etc.msvc):
	env_etc.Append(CCFLAGS="-std=c++11")
# -ffast-math seems to be incompatible with ec2comp on recent versions of
# GCC and Clang
if '-ffast-math' in env_etc['CCFLAGS']:
	env_etc['CCFLAGS'].remove('-ffast-math')

#!/usr/bin/env python

Import('env')
Import('env_modules')

env_upnp = env_modules.Clone()

# Thirdparty source files

if env['builtin_miniupnpc']:
    thirdparty_dir = "#thirdparty/miniupnpc/"
    thirdparty_sources = [
        "miniupnpc.c",
        "upnpcommands.c",
        "miniwget.c",
        "upnpdev.c",
        "igd_desc_parse.c",
        "minissdpc.c",
        "minisoap.c",
        "minixml.c",
        "connecthostport.c",
        "receivedata.c",
        "portlistingparse.c",
        "upnpreplyparse.c",
    ]
    thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

    env_upnp.Append(CPPPATH=[thirdparty_dir])
    env_upnp.Append(CPPFLAGS=["-DMINIUPNP_STATICLIB"])

    env_thirdparty = env_upnp.Clone()
    env_thirdparty.disable_warnings()
    env_thirdparty.add_source_files(env.modules_sources, thirdparty_sources)

# Godot source files
env_upnp.add_source_files(env.modules_sources, "*.cpp")

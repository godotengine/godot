#!/usr/bin/env python
from misc.utility.scons_hints import *

Import("env")
Import("env_modules")

freetype_enabled = "freetype" in env.module_list
msdfgen_enabled = "msdfgen" in env.module_list

env_text_server_fb = env_modules.Clone()

if "svg" in env.module_list:
    env_text_server_fb.AddExternalIncludes(
        ["#thirdparty/thorvg/inc", "#thirdparty/thorvg/src/common", "#thirdparty/thorvg/src/renderer"]
    )
    # Enable ThorVG static object linking.
    env_text_server_fb.Append(CPPDEFINES=["TVG_STATIC"])

if env["builtin_msdfgen"] and msdfgen_enabled:
    env_text_server_fb.Append(CPPDEFINES=[("MSDFGEN_PUBLIC", "")])
    env_text_server_fb.AddExternalIncludes("#thirdparty/msdfgen")

if env["builtin_freetype"] and freetype_enabled:
    env_text_server_fb.Append(CPPDEFINES=["FT_CONFIG_OPTION_USE_BROTLI"])
    env_text_server_fb.AddExternalIncludes("#thirdparty/freetype/include")

env_text_server_fb.add_source_files(env.modules_sources, "*.cpp")

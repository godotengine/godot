#!/usr/bin/env python

Import('env')
Import('env_modules')

env_tinyexr = env_modules.Clone()

# Thirdparty source files
# Not unbundled for now as they are not commonly available as shared library
thirdparty_dir = "#thirdparty/tinyexr/"
thirdparty_sources = [
    "tinyexr.cc",
]
thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_tinyexr.Prepend(CPPPATH=[thirdparty_dir])

env_thirdparty = env_tinyexr.Clone()
env_thirdparty.disable_warnings()
env_thirdparty.add_source_files(env.modules_sources, thirdparty_sources)

# Godot's own source files
env_tinyexr.add_source_files(env.modules_sources, "*.cpp")

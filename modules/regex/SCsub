#!/usr/bin/env python

Import('env')
Import('env_modules')

env_regex = env_modules.Clone()
env_regex.Append(CPPFLAGS=["-DPCRE2_CODE_UNIT_WIDTH=0"])
env_regex.add_source_files(env.modules_sources, "*.cpp")

if env['builtin_pcre2']:
    jit_blacklist = ['javascript', 'uwp']
    thirdparty_dir = '#thirdparty/pcre2/src/'
    thirdparty_flags = ['-DPCRE2_STATIC', '-DHAVE_CONFIG_H']
    if 'platform' in env and env['platform'] not in jit_blacklist:
        thirdparty_flags.append('-DSUPPORT_JIT')
    thirdparty_sources = [
        "pcre2_auto_possess.c",
        "pcre2_chartables.c",
        "pcre2_compile.c",
        "pcre2_config.c",
        "pcre2_context.c",
        "pcre2_dfa_match.c",
        "pcre2_error.c",
        "pcre2_find_bracket.c",
        "pcre2_jit_compile.c",
        "pcre2_maketables.c",
        "pcre2_match.c",
        "pcre2_match_data.c",
        "pcre2_newline.c",
        "pcre2_ord2utf.c",
        "pcre2_pattern_info.c",
        "pcre2_serialize.c",
        "pcre2_string_utils.c",
        "pcre2_study.c",
        "pcre2_substitute.c",
        "pcre2_substring.c",
        "pcre2_tables.c",
        "pcre2_ucd.c",
        "pcre2_valid_utf.c",
        "pcre2_xclass.c",
    ]
    thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]
    env_regex.Append(CPPPATH=[thirdparty_dir])
    env_regex.Append(CPPFLAGS=thirdparty_flags)
    def pcre2_builtin(width):
        env_pcre2 = env_modules.Clone()
        env_pcre2["OBJSUFFIX"] = "_" + width + env_pcre2["OBJSUFFIX"]
        env_pcre2.Append(CPPPATH=[thirdparty_dir])
        env_pcre2.add_source_files(env.modules_sources, thirdparty_sources)
        env_pcre2.Append(CPPFLAGS=thirdparty_flags)
        env_pcre2.Append(CPPFLAGS=["-DPCRE2_CODE_UNIT_WIDTH=" + width])
    pcre2_builtin("16")
    pcre2_builtin("32")

#!/usr/bin/env python

Import("env")
Import("env_modules")

env_svg = env_modules.Clone()

# Thirdparty source files

thirdparty_obj = []

thirdparty_dir = "#thirdparty/thorvg/"
thirdparty_sources = [
    "src/lib/sw_engine/tvgSwFill.cpp",
    "src/lib/sw_engine/tvgSwImage.cpp",
    "src/lib/sw_engine/tvgSwMath.cpp",
    "src/lib/sw_engine/tvgSwMemPool.cpp",
    "src/lib/sw_engine/tvgSwRaster.cpp",
    "src/lib/sw_engine/tvgSwRenderer.cpp",
    "src/lib/sw_engine/tvgSwRle.cpp",
    "src/lib/sw_engine/tvgSwShape.cpp",
    "src/lib/sw_engine/tvgSwStroke.cpp",
    "src/lib/tvgAccessor.cpp",
    "src/lib/tvgCanvas.cpp",
    "src/lib/tvgFill.cpp",
    "src/lib/tvgGlCanvas.cpp",
    "src/lib/tvgInitializer.cpp",
    "src/lib/tvgLoader.cpp",
    "src/lib/tvgPaint.cpp",
    "src/lib/tvgPicture.cpp",
    "src/lib/tvgRender.cpp",
    "src/lib/tvgSaver.cpp",
    "src/lib/tvgScene.cpp",
    "src/lib/tvgShape.cpp",
    "src/lib/tvgSwCanvas.cpp",
    "src/lib/tvgTaskScheduler.cpp",
    "src/utils/tvgBezier.cpp",
    "src/utils/tvgCompressor.cpp",
    "src/utils/tvgStr.cpp",
    "src/loaders/raw/tvgRawLoader.cpp",
    "src/loaders/svg/tvgSvgCssStyle.cpp",
    "src/loaders/svg/tvgSvgLoader.cpp",
    "src/loaders/svg/tvgSvgPath.cpp",
    "src/loaders/svg/tvgSvgSceneBuilder.cpp",
    "src/loaders/svg/tvgSvgUtil.cpp",
    "src/loaders/svg/tvgXmlParser.cpp",
]

thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_svg.Prepend(CPPPATH=[thirdparty_dir + "inc"])

# Enable ThorVG static object linking.
env_svg.Append(CPPDEFINES=["TVG_STATIC"])

env_thirdparty = env_svg.Clone()
env_thirdparty.disable_warnings()
env_thirdparty.Prepend(
    CPPPATH=[
        thirdparty_dir + "src/lib",
        thirdparty_dir + "src/lib/sw_engine",
        thirdparty_dir + "src/loaders/raw",
        thirdparty_dir + "src/loaders/svg",
        thirdparty_dir + "src/utils",
    ]
)
# Also requires libpng headers
if env["builtin_libpng"]:
    env_thirdparty.Prepend(CPPPATH=["#thirdparty/libpng"])

env_thirdparty.add_source_files(thirdparty_obj, thirdparty_sources)
env.modules_sources += thirdparty_obj

# Godot source files

module_obj = []

env_svg.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)

#!/usr/bin/env python

Import("env")

from platform_methods import run_in_subprocess
import default_theme_builders
import default_theme_icons_builders

env.add_source_files(env.scene_sources, "*.cpp")

env.Depends("#scene/resources/default_theme/default_font.gen.h", "#thirdparty/fonts/OpenSans_SemiBold.ttf")
env.CommandNoCache(
    "#scene/resources/default_theme/default_font.gen.h",
    "#thirdparty/fonts/OpenSans_SemiBold.ttf",
    run_in_subprocess(default_theme_builders.make_fonts_header),
)

env["BUILDERS"]["MakeDefaultThemeIconsBuilder"] = Builder(
    action=env.Run(
        default_theme_icons_builders.make_default_theme_icons_action, "Generating default project theme icons header."
    ),
    suffix=".h",
    src_suffix=".svg",
)

# Default theme icons
icon_sources = Glob("*.svg")

env.Alias(
    "default_theme_icons",
    [env.MakeDefaultThemeIconsBuilder("#scene/resources/default_theme/default_theme_icons.gen.h", icon_sources)],
)

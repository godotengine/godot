#!/usr/bin/env python

Import("env")

import os

import editor_icons_builders


env["BUILDERS"]["MakeEditorIconsBuilder"] = Builder(
    action=env.Run(editor_icons_builders.make_editor_icons_action, "Generating editor icons header."),
    suffix=".h",
    src_suffix=".svg",
)

# Editor's own icons
icon_sources = Glob("*.svg")

# Module icons
for path in env.module_icons_paths:
    if not os.path.isabs(path):
        icon_sources += Glob("#" + path + "/*.svg")  # Built-in.
    else:
        icon_sources += Glob(path + "/*.svg")  # Custom.

env.Alias("editor_icons", [env.MakeEditorIconsBuilder("#editor/editor_icons.gen.h", icon_sources)])

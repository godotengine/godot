#!/usr/bin/env python
from misc.utility.scons_hints import *

Import("env")

if env["platform"] in ["macos", "windows", "linuxbsd"]:
    # Thirdparty source files
    thirdparty_dir = "#thirdparty/glad/"
    thirdparty_sources = ["gl.c"]

    if not env.get("angle_libs"):
        thirdparty_sources += ["egl.c"]

    thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

    # Treat glad headers as system headers to avoid raising warnings. Not supported on MSVC.
    if not env.msvc:
        env.Append(CPPFLAGS=["-isystem", Dir(thirdparty_dir).path])
    else:
        env.Prepend(CPPPATH=[thirdparty_dir])

    env.Append(CPPDEFINES=["GLAD_ENABLED"])
    env.Append(CPPDEFINES=["EGL_ENABLED"])

    env_thirdparty = env.Clone()
    env_thirdparty.disable_warnings()
    env_thirdparty.add_source_files(env.drivers_sources, thirdparty_sources)

# Godot source files
env.add_source_files(env.drivers_sources, "*.cpp")

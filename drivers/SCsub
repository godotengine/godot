#!/usr/bin/env python

Import('env')

env.drivers_sources = []

if ("builtin_zlib" in env and env["builtin_zlib"] == "yes"):
    SConscript("zlib/SCsub")

# OS drivers
SConscript('unix/SCsub')
SConscript('windows/SCsub')

# Sounds drivers
SConscript('alsa/SCsub')
SConscript('pulseaudio/SCsub')
if (env["platform"] == "windows"):
    SConscript("rtaudio/SCsub")
    SConscript("wasapi/SCsub")

# Graphics drivers
SConscript('gles2/SCsub')
SConscript('gl_context/SCsub')

# Core dependencies
SConscript("png/SCsub")
SConscript("nrex/SCsub")

# Tools override
# FIXME: Should likely be integrated in the tools/ codebase
if (env["tools"] == "yes"):
    SConscript("convex_decomp/SCsub")

if env['vsproj'] == "yes":
    env.AddToVSProject(env.drivers_sources)

if env.split_drivers:
    env.split_lib("drivers")
else:
    env.add_source_files(env.drivers_sources, "*.cpp")
    lib = env.add_library("drivers", env.drivers_sources)
    env.Prepend(LIBS=[lib])
